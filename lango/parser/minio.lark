start: (prelude stmt (sep stmt)*)? postlude

prelude: (_NL | COMMENT)*
sep: (_NL | COMMENT)+
postlude: (_NL | COMMENT)*

?stmt: data_decl
     | func_def
     | func_sig

// --- Data Declarations ---
data_decl: "data" TYPE "=" constructor ("|" constructor)*

constructor: UIDENT record_constructor
           | UIDENT type_expr*

record_constructor: "{" [field ("," field)*] "}"
field: ID "::" type_expr

// --- Function Signatures and Definitions ---
func_sig: ID "::" type_expr ("->" type_expr)*

func_def: ID pattern* "=" expr

// --- Expressions ---
// Arithmetic
PLUS: "+"
MINUS: "-"
MUL: "*"
DIV: "/"
POW_INT: "^"
POW_FLOAT: "^^"|"**"
// Comparison
EQ: "=="
NEQ: "/="
LT: "<"
LTEQ: "<="
GT: ">"
GTEQ: ">="
// Logical
AND: "&&"
OR: "||"
NOT: "not"

do_stmt: "let" ID "=" expr+ (";" ID "=" expr)* "in" _NL? expr
    | expr

stmt_list: (do_stmt (_NL | ";")*)+

?expr: "do" _NL? "{" _NL? stmt_list _NL? "}"  -> do_block
     | "if" expr _NL? "then" _NL? expr _NL? "else" _NL? expr -> if_else
     | expr "++" expr             -> concat
     | expr "$" expr              -> dollar
     | expr PLUS expr             -> add
     | expr MINUS expr            -> sub
     | expr MUL expr              -> mul
     | expr DIV expr              -> div
     | expr POW_INT expr          -> pow_int
     | expr POW_FLOAT expr        -> pow_float
     | expr EQ expr               -> eq
     | expr NEQ expr              -> neq
     | expr LT expr               -> lt
     | expr LTEQ expr             -> lteq
     | expr GT expr               -> gt
     | expr GTEQ expr             -> gteq
     | expr AND expr              -> and
     | expr OR expr               -> or
     | NOT expr                   -> not
     | app_expr

?app_expr: app_expr atom_expr      -> app
         | atom_expr

?atom_expr: "(" expr ")"           -> grouped
          | "(" MINUS INT ")"      -> neg_int
          | "(" MINUS FLOAT ")"    -> neg_float
          | constructor_expr
          | UIDENT                 -> constructor
          | ID                     -> var
          | literal

constructor_expr: UIDENT "{" field_assign ("," field_assign)* "}"
field_assign: ID "=" expr

// --- Types ---
?type_expr: TYPE
          | TYPE "->" type_expr

// --- Patterns ---
?pattern: constructor_pattern
        | ID
        | literal
        | "(" MINUS INT ")"      -> neg_int
        | "(" MINUS FLOAT ")"    -> neg_float

constructor_pattern: "(" UIDENT pattern+ ")"

// --- Literals ---
?literal: INT     -> int
        | FLOAT   -> float
        | STRING  -> string
        | "True"  -> true
        | "False" -> false

// --- Tokens ---
ID: /[a-z][a-zA-Z0-9_]*/
TYPE: /[A-Z][a-zA-Z0-9_]*/
UIDENT: TYPE
STRING: /"[^"]*"/
INT: /[0-9]+/
FLOAT: /[0-9]+\.[0-9]+/

COMMENT: /--.*/
%ignore COMMENT
%ignore /[ \t]+/
_NL: /(\r?\n)+/

// TODO: remove _NL -> switch from WS_INLINE to WS

%import common.WS_INLINE
%ignore WS_INLINE
